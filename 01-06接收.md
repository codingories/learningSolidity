三个接收ETH相关的关键字: payable fallback receive
- payable 标记函数或者地址，接收以太坊的主币，2300 gas 的限制，能不加就不加
    - function 方法可以执行接收以太
    - address 可以向地址转钱
- 深红色按钮可以发送以太币 

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract Payable {
    // payable 标记函数，可以让这个方法接收以太坊
    function deposit1() external payable {

    }

    function deposit2() external {}

    // payable 标记地址
    function withdraw() external {
        // msg.sender表示当前合约的调用者
        // address(this).balance表示当前合约的所有余额
        // .transfer表示转到msg.sender这里，to地址写在前，from地址就是当前的合约
        payable(msg.sender).transfer(address(this).balance);
    }

    // 通过 balance 属性，来看余额
    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}
```

- fallback
    - 语法: 
        - `fallback () external [payable]`, payable可写可补血
        - `fallback (bytes calldata input) external [payable] returns (bytes memory output)`，input可写可不写，通过msg.data来获取
        - msg.data 可以通过 abi.decode[4:]解码
    - 回退函数在两种情况下调用
        - 向合约转账
        - 执行合约不存在的方法
    - 带参数的fallback
- receive: 只负责接收主币，一个合约只有一个fallback，一个合约只有一个receive
    - `receive() external payable {}` // 必须要有payable属性
    - payable 是必须的
    - 没有 function 关键字


fallback例子，演示可以通过calldata转入
```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract Payable {
   event Log(string funName, address from, uint256 value, bytes data);
   function deposit() external payable {}
   // 通过balance属性，查看余额
   function getBalance() external view returns (uint256) {
       return address(this).balance;
   }

   fallback() external payable {
       emit Log("fallback", msg.sender, msg.value, msg.data);
   }
}
```
演示fallback执行合约不存在函数的方法
```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

// 0xd8b934580fcE35a11B58C6D73aDeE468a2833fa8
contract StoneCat {
    uint256 public age = 0;
    event eventFallback(string);

    // 发送到这个合约的所有消息都会调用此函数（因为该合约没有其它函数）。
    // 向这个合约发送以太币会导致异常，因为 fallback 函数没有 `payable` 修饰符
    // fallback是一个兜底的函数
    // 当fallback执行不存在的方法可以被调用
    fallback() external {
        age++;
        emit eventFallback("fallbak");
    }
}

interface AnimalEat {
    function eat() external returns (string memory);
}

contract Animal {
    // StoneCat address
    // 推荐这种写法
    function test1(address _addr) external returns (string memory) {
        // 通过接口把_addr包装去来
        AnimalEat general = AnimalEat(_addr);
        // 执行合约上的eat方法
        return general.eat();
    }

    // 第二种写法，有点像直接调用CALLDATA，函数内会做一个检测，CALL方法会绕过合约的检测，是比较底层的方法
    // 需要知道: test1和test2两个的方法是一样的
    // 直接写方法是不行的，但是通过.call这种方法可以绕过合约的检测，不检查的，官方不推荐，
    function test2(address _addr) external returns (bool success) {
        AnimalEat general = AnimalEat(_addr);
        (success,) = address(general).call(abi.encodeWithSignature("eat()"));
        require(success);
    }
}
```

带参数的fallback
```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract Demo {
    bytes public inputData1;
    bytes public inputData2;

    // 类型只能是bytes
    fallback (bytes calldata input) external  returns (bytes memory output){
        inputData1 = input;
        inputData2 = msg.data; // input 等于 msg.data，可以用abi.decode解出来
        return input;
    }
}
```

abi.decode演示
```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract StoneCat {
    uint256 public age = 0;
    bytes public inputData1;
    bytes public inputData2;
    uint256 public c;
    uint256 public d;

    event eventFallback(string);

    fallback (bytes calldata input) external  returns (bytes memory output){
        age++;
        inputData1 = input;
        inputData2 = msg.data;
        // 从第四位开始切片
        // abi.decode可以对调用合约的数据来进行一些解析，前面四个字节是函数的签名，
        (c, d) = abi.decode(msg.data[4:], (uint256, uint256));
        emit eventFallback("fallbak");
        return input;
    }
}

interface AnimalEat {
    function eat() external returns (string memory);
}

contract Animal {
    function test2(address _addr) external returns (bool success) {
        AnimalEat general = AnimalEat(_addr);
        (success, ) = address(general).call(abi.encodeWithSignature("eat()",123,456));
        require(success);
    }
}
```

- 通过callDATA转账调用，会出发receive函数，如果有参数比如0X00就会报错
```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract Demo {
    event Log(string funName, address from, uint256 value, bytes data);

    receive() external payable {
        // receive 被调用的时候不存在 msg.data，所以不使用这个，直接用空字符串
        emit Log("receive", msg.sender, msg.value, "");
    }

    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}
```

- receive 和 fallback 共存的调用
```js
/**

    调用时发送了ETH
            |
判断 msg.data 是否为空
          /     \
        是       否
是否存在 receive   fallbak()
      /   \
    存在   不存在
    /        \
receive()   fallbak()

 */
```
例子
```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

// 这个合约会保留所有发送给它的以太币，没有办法返还。
contract Demo {
    uint256 public x;
    uint256 public y;

    event Log(string funName, address from, uint256 value, bytes data);

    // 纯转账调用这个函数，例如对每个空empty calldata的调用
    receive() external payable {
        x = 1;
        y = msg.value;
        emit Log("receive", msg.sender, msg.value, "");
    }

    // 除了纯转账外，所有的调用都会调用这个函数．
    // (因为除了 receive 函数外，没有其他的函数).
    // 任何对合约非空calldata 调用会执行回退函数(即使是调用函数附加以太).
    fallback() external payable {
        x = 2;
        y = msg.value;
        emit Log("fallback", msg.sender, msg.value, msg.data);
    }

    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}
```